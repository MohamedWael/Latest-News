apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.github.mohamedwael.latestnews"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies { config ->
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'

    def navigation_version = "2.1.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    def paging_version = "2.1.0"
    implementation "androidx.paging:paging-runtime:$paging_version"
    testImplementation "androidx.paging:paging-common:$paging_version"

    def coroutinesVersion = "1.2.2"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    roomDependencies(config)
    networkLayerDependencies(config)
    testImplementation "org.mockito:mockito-core:3.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

private static void roomDependencies(config) {
    def room_version = "2.2.0-rc01"
    config.implementation "androidx.room:room-runtime:$room_version"
    config.kapt "androidx.room:room-compiler:$room_version"
    config.implementation "androidx.room:room-ktx:$room_version"
    config.testImplementation "androidx.room:room-testing:$room_version"

}

private static void networkLayerDependencies(config) {
    config.implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    config.implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    config.implementation 'com.squareup.okhttp3:logging-interceptor:3.14.2'
    config.implementation 'com.squareup.okhttp3:okhttp:3.14.2'
    config.implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.0'
    config.implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}
